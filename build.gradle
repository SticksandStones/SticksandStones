buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'


repositories {
    maven {
        name = "Modmuss50"
        url = "http://modmuss50.me/maven/"
    }
}

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = "0.1." + "${System.getenv().BUILD_NUMBER}"
} else {
    version = "0.1.BUILD"
}

minecraft {
    version = "1.7.10-10.13.3.1403-1.7.10"
    replace "@MODVERSION@", project.version
    replaceIn "Reference.java"
    replaceIn "package-info.java"
}

group = 'SticksandStones'

task deleteOldArtifacts(type: Delete) {
    delete 'build/libs'
}

classes.dependsOn deleteOldArtifacts

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "@MODVERSION@", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include '*.info'

                // replace version and mcversion
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude '**/*.info'
            }
        }

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
    classifier = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'dev'
    exclude "**/*.psd"
    classifier = 'dev'
}

jar {
    exclude "**/*.psd"
    appendix = 'universal'
    classifier = 'universal'
}

task apiJar(type: Jar) {
    from(sourceSets.main.java) {
        include 'com/SticksandStones/SandS/api/**'
        appendix = 'api'
        classifier = "api"
    }
}

apiJar.mustRunAfter deobfJar

artifacts {
    archives sourceJar
    archives deobfJar
    archives apiJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven/")
            pom {
                groupId = "SticksandStones"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'SticksandStones'
                    url 'https://github.com/SticksandStones/SticksandStones'
                    scm {
                        url 'https://github.com/SticksandStones/SticksandStones'
                        connection 'scm:git:git@github.com:SticksandStones/SticksandStones.git'
                        developerConnection 'scm:git:git@github.com:SticksandStones/SticksandStones.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/SticksandStones/SticksandStones/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://raw.githubusercontent.com/SticksandStones/SticksandStones/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    //To update the wrapper change the version bellow then run: gradle wrapper
    gradleVersion = '2.3'
}